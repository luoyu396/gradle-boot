plugins {
    id 'org.springframework.boot' version '2.1.18.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.8.RELEASE' apply false
    id 'java'
}

allprojects {
    //项目是idea项目
    apply plugin: 'idea'
    //统一公共属性
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'
    ext {
        //这里管理版本
        swagger2Version = '3.0.0'
        springCloudVersion = "Greenwich.SR2"
        cloudAlibabaVersion = "2.1.1.RELEASE"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    // 指定JDK版本
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    // 指定编码格式
    [compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

    repositories {
        mavenLocal()
        maven {
            url 'https://maven.aliyun.com/nexus/content/groups/public/' //配置阿里云 或者 自己公司的仓库
        }
        mavenCentral()
    }

    dependencies {
        implementation "io.springfox:springfox-swagger2:${swagger2Version}"
        implementation "io.springfox:springfox-swagger-ui:${swagger2Version}"
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation group: 'com.baomidou', name: 'mybatis-plus-boot-starter', version: '3.3.1'
        implementation "com.baomidou:mybatis-plus-generator:3.3.2"
        implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.28'
        implementation 'cn.hutool:hutool-all:5.7.13'
        implementation group: 'com.github.jsqlparser', name: 'jsqlparser', version: '2.1'
        implementation group: 'com.github.pagehelper', name: 'pagehelper-spring-boot-starter', version: '1.3.0'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${cloudAlibabaVersion}"
        }
    }
}

